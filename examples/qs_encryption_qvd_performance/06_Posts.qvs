// Pick which data set
LET vConn_Test_Data = '$(vConn_QVD_Server)';
LET vConn_Test_File = 'Posts';

// Create table
LET vConn_Test_Table = lower('$(vConn_Test_File)');

// Loop over test
FOR iter_Test = 0 to vTest_Iterations-1

	/////////// Raw load
    [$(vConn_Test_File)]:
    LOAD
        Id,
        PostTypeId,
        AcceptedAnswerId,
        CreationDate,
        Score,
        ViewCount,
        Body,
        OwnerUserId,
        LastEditorUserId,
        LastEditDate,
        LastActivityDate,
        Title,
        "Tags",
        AnswerCount,
        CommentCount,
        FavoriteCount,
        ParentId,
        ClosedDate,
        CommunityOwnedDate,
        OwnerDisplayName,
        LastEditorDisplayName
    FROM [$(vConn_Test_Data)/$(vConn_Test_File).xml]
    (XmlSimple, table is [$(vConn_Test_Table)/row]);
   
    Call sTimerEnd('$(vConn_Test_File) raw load [$(vConn_Test_Data)])');
    
    /////////// Badge raw store & drop
    STORE [$(vConn_Test_File)] INTO [$(vConn_Test_Data)/$(vConn_Test_File).qvd] (qvd);
    DROP TABLE [$(vConn_Test_File)];
    
    Call sTimerEnd('$(vConn_Test_File) raw store & drop [$(vConn_Test_Data)])');
    
    
    /////////// QVD optimised load
	[$(vConn_Test_File)]:
    LOAD
        *
    FROM [$(vConn_Test_Data)/$(vConn_Test_File).qvd] (qvd);
    
	Call sTimerEnd('$(vConn_Test_File) QVD optimised load [$(vConn_Test_Data)])');
    
    /////////// Badge second store & drop
    STORE [$(vConn_Test_File)] INTO [$(vConn_Test_Data)/$(vConn_Test_File)_copy.qvd] (qvd);
    DROP TABLE [$(vConn_Test_File)];
    
    Call sTimerEnd('$(vConn_Test_File) QVD store and drop [$(vConn_Test_Data)])');
    
    /////////// Unoptimised load and store
    [$(vConn_Test_File)]:
    LOAD
        Id							AS [Post ID],
        PostTypeId					AS [Post Type ID],
        AcceptedAnswerId			AS [Post Accepted Answer ID],
        Timestamp(CreationDate)		AS [Post Created Timestamp],
        Date(Floor(CreationDate))	AS [Post Created Date],
        Score						AS [Post Score],
        ViewCount					AS [Post View Count],
        Body						AS [Post Body],
        Hash256(Body)				AS [Post Body Hash],
        OwnerUserId					AS [Post Owner User ID],
        LastEditorUserId			AS [Post Last Editor User ID],
        Timestamp(LastEditDate)		AS [Post Last Edit Timestamp],
        Date(Floor(LastEditDate))	AS [Post Last Edit Date],
        Timestamp(LastActivityDate)		AS [Post Last Activity Timestamp],
        Date(Floor(LastActivityDate))	AS [Post Last Activity Date],
        Title						AS [Post Title],
        "Tags"						AS [Post Tags],
        AnswerCount					AS [Post Answer Count],
        CommentCount				AS [Post Comment Count],
        FavoriteCount				AS [Post Favourite Count],
        ParentId					AS [Post Parent ID],
        Timestamp(ClosedDate)		AS [Post Closed Timestamp],
        Date(Floor(ClosedDate))		AS [Post Closed Date],
        Timestamp(CommunityOwnedDate)		AS [Post Community Owned Timestamp],
        Date(Floor(CommunityOwnedDate))		AS [Post Community Owned Date],
        OwnerDisplayName			AS [Post Owner Name],
        LastEditorDisplayName		AS [Post Last Editor Name]
    FROM [$(vConn_Test_Data)/$(vConn_Test_File).qvd]
    (qvd);
    
    Call sTimerEnd('$(vConn_Test_File) QVD unoptimised load & transform 1 (load) [$(vConn_Test_Data)])');

    STORE [$(vConn_Test_File)] INTO [$(vConn_Test_Data)/$(vConn_Test_File)_ult1.qvd] (qvd);
    DROP TABLE [$(vConn_Test_File)];
    
    Call sTimerEnd('$(vConn_Test_File) QVD unoptimised load & transform 1 (store) [$(vConn_Test_Data)])');
    
    /////////// Unoptimised load and store #2
    [$(vConn_Test_File)]:
    LOAD
        *,
        Hash128(Id)	AS IdHash
    FROM [$(vConn_Test_Data)/$(vConn_Test_File).qvd]
    (qvd)
    WHERE WildMatch([Body],'*app*') OR WildMatch(Id,'*1*');
    
    Call sTimerEnd('$(vConn_Test_File) QVD unoptimised load & transform 2 (load) [$(vConn_Test_Data)])');

    STORE [$(vConn_Test_File)] INTO [$(vConn_Test_Data)/$(vConn_Test_File)_ult2.qvd] (qvd);
    DROP TABLE [$(vConn_Test_File)];
    
    Call sTimerEnd('$(vConn_Test_File) QVD unoptimised load & transform 2 (store) [$(vConn_Test_Data)])');
    
    /////////// Optimised load and resident then store
    [$(vConn_Test_File)_Temp]:
    LOAD
        *
    FROM [$(vConn_Test_Data)/$(vConn_Test_File).qvd]
    (qvd);
    
    [$(vConn_Test_File)]:
    NoConcatenate
    LOAD
        Id							AS [Post ID],
        PostTypeId					AS [Post Type ID],
        AcceptedAnswerId			AS [Post Accepted Answer ID],
        Timestamp(CreationDate)		AS [Post Created Timestamp],
        Date(Floor(CreationDate))	AS [Post Created Date],
        Score						AS [Post Score],
        ViewCount					AS [Post View Count],
        Body						AS [Post Body],
        Hash256(Body)				AS [Post Body Hash],
        OwnerUserId					AS [Post Owner User ID],
        LastEditorUserId			AS [Post Last Editor User ID],
        Timestamp(LastEditDate)		AS [Post Last Edit Timestamp],
        Date(Floor(LastEditDate))	AS [Post Last Edit Date],
        Timestamp(LastActivityDate)		AS [Post Last Activity Timestamp],
        Date(Floor(LastActivityDate))	AS [Post Last Activity Date],
        Title						AS [Post Title],
        "Tags"						AS [Post Tags],
        AnswerCount					AS [Post Answer Count],
        CommentCount				AS [Post Comment Count],
        FavoriteCount				AS [Post Favourite Count],
        ParentId					AS [Post Parent ID],
        Timestamp(ClosedDate)		AS [Post Closed Timestamp],
        Date(Floor(ClosedDate))		AS [Post Closed Date],
        Timestamp(CommunityOwnedDate)		AS [Post Community Owned Timestamp],
        Date(Floor(CommunityOwnedDate))		AS [Post Community Owned Date],
        OwnerDisplayName			AS [Post Owner Name],
        LastEditorDisplayName		AS [Post Last Editor Name]
    RESIDENT [$(vConn_Test_File)_Temp];
    
    DROP TABLE [$(vConn_Test_File)_Temp];
    
    Call sTimerEnd('$(vConn_Test_File) QVD optimised load & transform 1 (load) [$(vConn_Test_Data)])');
    
    STORE [$(vConn_Test_File)] INTO [$(vConn_Test_Data)/$(vConn_Test_File)_olt1.qvd] (qvd);
    DROP TABLE [$(vConn_Test_File)];
    
    Call sTimerEnd('$(vConn_Test_File) QVD optimised load & transform 1 (store) [$(vConn_Test_Data)])');

Next iter_Test;